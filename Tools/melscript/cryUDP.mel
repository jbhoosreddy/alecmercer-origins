global int $g_UDPSelectionChangedJob = -1;
global proc UDPKillJobs()
{
	global int $g_UDPSelectionChangedJob;
	if( $g_UDPSelectionChangedJob != -1 )
	{
		scriptJob -force -kill $g_UDPSelectionChangedJob;
		$g_UDPSelectionChangedJob = -1;
	}
}

global proc UDPCloseWindow()
{
	if(`window -ex CRYUDP_WINDOW`) 
		deleteUI -window CRYUDP_WINDOW;
}

global proc UDPSave()
{
	string $selected[];
	$selected = `ls -sl`;
	if( size($selected) > 0 )
	{
		string $node = $selected[0];

		// Add the UDP attribute
		if( !`attributeExists "UDP" $node` )
			addAttr -ln UDP -dt "string" $node;

		string $value = `scrollField -q -text UDP_LINES`;
		setAttr -type "string" ($node+".UDP") $value;

		button -e -enable false UDP_SAVEBUTTON;
	}
}

global proc UDPUpdate()
{
	int $set = 0;
	string $selected[];
	$selected = `ls -sl`;
	if( size($selected) > 0 )
	{
		string $node = $selected[0];

		if( `attributeExists "UDP" $node` )
		{
			string $value = `getAttr ($node+".UDP")`;
			scrollField -e -text $value UDP_LINES;
			$set = 1;
		}		
	}
	if( $set == 0 )
		scrollField -e -text "" UDP_LINES;

	button -e -enable false UDP_SAVEBUTTON;
}

global proc cryUDPWindow( )
{
	global int $g_UDPSelectionChangedJob;
	if(!`window -ex CRYUDP_WINDOW`) 
	{
		if(`windowPref -exists CRYUDP_WINDOW`) 
		{
			windowPref -wh 350 210 -tlc `windowPref -q -topEdge CRYUDP_WINDOW` `windowPref -q -leftEdge CRYUDP_WINDOW` CRYUDP_WINDOW;
		}
		window -titleBar true -title "User Defined Properties" -widthHeight 350 210 -sizeable false -mnb false -mxb false CRYUDP_WINDOW;

		$layout1 = `formLayout -numberOfDivisions 100`;

		scrollField -keyPressCommand ("button -e -enable true UDP_SAVEBUTTON") UDP_LINES;

		button -label "Save" -command ("UDPSave") UDP_SAVEBUTTON;
		button -label "Close" -command ("UDPCloseWindow") UDP_CLOSEBUTTON;
		setParent ..;

		formLayout -edit
			-attachForm UDP_LINES "top" 5
			-attachForm UDP_LINES "left" 5
			-attachForm UDP_LINES "right" 5
			-attachControl UDP_LINES "bottom" 5 UDP_SAVEBUTTON

			-attachControl UDP_SAVEBUTTON "bottom" 5 UDP_CLOSEBUTTON 
			-attachForm UDP_SAVEBUTTON "left" 5
			-attachForm UDP_SAVEBUTTON "right" 5
			-attachNone UDP_SAVEBUTTON "top"

			-attachForm UDP_CLOSEBUTTON "bottom" 5 
			-attachForm UDP_CLOSEBUTTON "left" 5
			-attachForm UDP_CLOSEBUTTON "right" 5
			-attachNone UDP_CLOSEBUTTON "top"
		$layout1;

		UDPUpdate();

		showWindow CRYUDP_WINDOW;	

		$g_UDPSelectionChangedJob = `scriptJob -event "SelectionChanged" "UDPUpdate"`;
		scriptJob -uiDeleted "CRYUDP_WINDOW" "UDPKillJobs";
	}
}